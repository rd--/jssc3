<h1>CoinGate - statistical gate</h1><p><em>CoinGate(prob, trig)</em></p><p>When it receives a trigger, it tosses a coin, and either passes the trigger or doesn't.</p><ul><li>prob: value between 0 and 1 determines probability of either possibilities</li><li>trig: input signal</li></ul><p>Mouse controls probablity:</p><pre><code>var prob = MouseX(0, 1, 0, 0.2);
var trig = CoinGate(prob, Impulse(10, 0));
SinOsc(TRand([200, 300], [400, 900], trig), 0) * 0.1
</code></pre><p>Trigger level is preserved, mouse controls probablity over partial range:</p><pre><code>var prob = MouseX(0, 0.65, 0, 0.2);
var trig = Impulse(20, 0) * (SinOsc(0.5, 0) + 1);
{ Ringz(CoinGate(prob, trig * 0.5), #[1, 1.5] * Rand(1000, 9000), 0.01) }.dup(3).sum
</code></pre><h1>ExpRand</h1><p><em>ExpRand(lo, hi)</em></p><p>Generates a single random float value in an exponential distributions from lo to hi.</p><pre><code>var n = 5;
var o = SinOsc({ ExpRand(110, 220) }.dup(n), 0) * ({ ExpRand(0.05, 0.10) }.dup(n));
Splay2(o)
</code></pre><h1>IRand</h1><p><em>IRand(lo, hi)</em></p><p>Generates a single random integer value in uniform distribution from lo to hi</p><pre><code>var scale = [0, 2, 4, 5, 7, 9, 10, 12].asLocalBuf;
{
    var degree = IRand(0, 7);
    var octave = IRand(4, 7);
    var pitchClass = Index(scale, degree);
    var mnn = (octave * 12) + pitchClass;
    var numHarm = IRand(1, 4);
    Blip(mnn.midiCps, numHarm) * 0.1
}.dup(7).splay2
</code></pre><h1>LinRand</h1><p><em>LinRand(lo, hi, minmax)</em></p><p>Generates a single random float value in linear distribution from lo to hi, skewed towards lo if minmax &lt; 0, otherwise skewed towards hi.</p><pre><code>var minmax = MouseX(0, 1, 0, 0.2);
{
    var freq = LinRand(200, 10000, minmax);
    var dur =  (1 / freq) * 7500;
    FSinOsc(freq, 0) * Ln(0.2, 0, dur)
}.dup(15).splay2
</code></pre><h1>NRand</h1><p><em>NRand(lo, hi, n)</em></p><p>Generates a single random float value in a sum of n uniform distributions from lo to hi.</p><ul><li>n = 1 : uniform distribution - same as Rand</li><li>n = 2 : triangular distribution</li><li>n = 3 : smooth hump</li></ul><p>As n increases, distribution converges towards gaussian.</p><pre><code>var n = MouseX(1, 9, 0, 0.2).rounded;
{
    var freq = NRand(200, 10000, n);
    var dur =  (1 / freq) * 7500;
    FSinOsc(freq, 0) * Ln(0.2, 0, dur)
}.dup(15).splay2
</code></pre><h1>Rand</h1><p><em>Rand(lo, hi)</em></p><p>Generates a single random float value in uniform distribution from lo to hi. It generates this when the SynthDef first starts playing, and remains fixed for the duration of the synth's existence.</p><pre><code>{
    var freq = Rand(200, 800);
    var dur =  (1 / freq) * 7500;
    FSinOsc(freq, 0) * Ln(0.2, 0, 1)
}.dup(5).splay2
</code></pre><h1>TExpRand - triggered exponential random number generator</h1><p><em>TExpRand(lo, hi, trig)</em></p><p>Generates a random float value in exponential distribution from lo to hi each time the trig signal changes from nonpositive to positive values lo and hi must both have the same sign and be non-zero.</p><pre><code>var trig = Dust(10);
SinOsc(TExpRand(300, 3000, trig), 0) * 0.1
</code></pre><p>Mouse controls density:</p><pre><code>var trig = Dust(MouseX(1, 8000, 1, 0.2));
SinOsc(TExpRand(300, 3000, trig), 0) * 0.1
</code></pre><h1>TIRand - triggered integer random number generator</h1><p><em>TIRand(lo, hi, trig)</em></p><p>Generates a random integer value in uniform distribution from lo to hi each time the trig signal changes from nonpositive to positive values</p><pre><code>var trig = Dust(10);
SinOsc(TIRand(4, 12, trig) * 100, 0) * 0.1
</code></pre><p>Mouse controls density:</p><pre><code>var trig = Dust(MouseX(1, 8000, 1, 0.2));
SinOsc(TIRand(4, 12, trig) * 100, 0) * 0.1
</code></pre><p>Random degree, octave and number of harmonics:</p><pre><code>var scale = [0, 2, 4, 5, 7, 9, 10, 12].asLocalBuf;
OverlapTexture({
    arg tr;
    {
        var degree = TIRand(0, 7, tr);
        var octave = TIRand(4, 7, tr);
        var pitchClass = Index(scale, degree);
        var mnn = (octave * 12) + pitchClass;
        var numHarm = TIRand(1, 4, tr);
        Blip(mnn.midiCps, numHarm) * 0.05
    }.dup(7).splay2
}, 4, 0.05, 2)
</code></pre><h1>TRand - triggered random number generator</h1><p><em>TRand(lo, hi, trig)</em></p><p>Generates a random float value in uniform distribution from lo to hi each time the trig signal changes from nonpositive to positive values</p><pre><code>var trig = Dust(10);
SinOsc(TRand(300, 3000, trig), 0) * 0.1
</code></pre><p>Mouse controls density:</p><pre><code>var trig = Dust(MouseX(1, 8000, 1, 0.2));
SinOsc(TRand(300, 3000, trig), 0) * 0.1
</code></pre>
