# 0.3 Minimal Syntax Overview

SuperCollider programming language is most closely related to Smalltalk-80, with some syntactical elements of the C-family thrown in.  This reference card is intended as a quick introduction for the very impatient.  All the details are covered later in the materials.

_Comments:_

    /* Anything enclosed between '/*' and '*/' is a comment; they nest. */
    // anything after '//' to the end of the line is also a comment.

_Variable Declarations:_

    var myVariable = 7; // variable names start with a lower-case letter

_Assignments:_

    a = 1; // an integer
    b = [5, 4, 3, 2, 1]; // a list
    myTestOsc = Saw.ar(freq, amp); // an audio-rate saw-tooth generator

_Statement Separator:_

    a = 1;  (a + 5).postln;

## Literals

Literals are values which have a direct syntactic representation.

_Numbers:_

An integer is any series of digits optionally preceded by a minus sign.  examples of integers :

    -13      	666      	2112      	96

A float is one or more decimal digits followed by a decimal point followed by one or more decimal digits.  You must have digits on both sides of the decimal point.  This distinguishes floating point numbers from integer expressions like:

	8.rand

examples of floats :

    0.39      	98.6      	1.0      	-0.5

Exponential notation is also supported.

    1.2e4		1e-4

The constant pi can be appended to a number to create floating point constant:

    2pi		0.5pi

Numbers can also be written in radices other than base 10 up to base 36.  The radix is specified in base 10 followed by the letter 'r' followed by the value written in that radix using characters 0-9,A-Z, or a-z, for digit values from 0 to 35.  For example you can write hexadecimal numbers as follows:

    16rF0
    16rA9FF

Binary numbers can be written as follows:

    2r01101011

Floating point values may also be specified in any base:

    12r4A.A

_Characters:_

Characters values are preceded by a dollar sign:

    $A

Tab, linefeed, carriage return, and backslash are preceded by a backslash:

    $\t		$\n		$\r		$\\

_Symbols:_

A symbol is written as a string enclosed in single quotes. Examples of symbols:

    'x'
	'aiff'
	'BigSwiftyAndAssoc'
	'nowhere here'
	'somewhere there'
	'.+o*o+.'

A symbol consisting of a single word can be written with a preceding backslash.

    \x
	\aiff
	\BigSwiftyAndAssoc

_Strings:_

Strings are written in double quotes:

    "This is a string."

If two or more strings are lexically adjacent, then they combine into a larger string.

    "This" " is " "also a " "string."

_Identifiers:_

Names of methods and variables begin with a lower case alphabetic character, followed by zero or more alphanumeric characters.

    var abc, z123, func;

_Class Names:_

Class names always begin with a capital letter followed  by zero or more  alphanumeric characters.

    Object	Point		Synth

_Special Values:_

The singular instances of the classes True, False, Nil and Infinitum are written as the words true, false, nil and inf.

_Literal Arrays:_

Arrays of literals are created at compile time and are written with a # preceding the array as follows:

    #[1, 2, 'abc', "def", 4]

Literal arrays must be used as is and may not be altered at run time.  When nesting literal arrays, only the outermost literal array needs the '#' character.

    #[[1, 2, 3], [4, 5, 6]]

## Messages

_Binary Operator messages:_

These consist of two expressions and a binary operator in the middle.

    2 + 3;
    (FSinOsc.ar(220) + PinkNoise.ar) * 0.2;

_Keyword messages:_

Message passing style (as in OO programming languages)

    "Hi there!".postln; // <SomeObject>.<someMessage>;
    23.max(45); // <SomeObject>.<someMessage>(<firstArgument>, <second>, ...);

Functional style (as in C) is completely equivalent:

    postln("Lo there!"); // <someFunction>(<SomeObject>);
    max(23, 45); // <someFunction>(<SomeObject>, <firstArgument>, <second>, ...);
    sin(-1); //  if you prefer that to    -1.sin;

Examples:

    f(a, b)    is equivalent to :   a.f(b)
	sin(x)    is equivalent to :   x.sin
	so:				g(f(a, b), c)
	is equivalent to :		g(a.f(b), c)
	is equivalent to :		f(a, b).g(c)
	is equivalent to :		a.f(b).g(c)

_Default Arguments:_

As an example, the function x is declared to take two arguments, a and b with default values given in the function declaration. It returns their sum, and can be called by sending it the message .value().

    x = { arg a=1, b=2; a + b };
    z = x.value; // z is set to 3. (a defaults to 1, b defaults to 2)
    z = x.value(10); // z is set to 12. (a is 10, b defaults to 2)
    z = x.value(10, 5); // z is set to 15. (a is 10, b is 5)
    z = x.value(10, 5, 9); // z is set to 15. (a is 10, b is 5, 9 is ignored)

_Keyword Arguments:_

Arguments to Methods may be specified by the name by which they are declared in a method's definition, and may be specified in any order.

For example the 'ar' class method of the SinOsc class takes 4 arguments named freq, phase, mul, and add in that order. All of the following are legal calls to that method.

    SinOsc.ar(800, pi, 0.2, 0); // supply all 4 arguments: freq, phase, mul, add
    SinOsc.ar(freq: 800, mul: 0.2); // otherwise unspecified arguments get default values.
    SinOsc.ar(phase: pi, mul: 0.2, freq: 800); // add gets its default value of zero.

Function args may be specified by keyword when using the special methods .value() and .perform().

    { arg a=1, b=2, c=3; [a, b, c].postln }.value(b: 7, c: 8);

## Terseness vs. Readability

Create an oscillator object inside a Function object and play the Function

    var myFunction;
    myFunction = { FSinOsc.ar(220, 0.2) };
    myFunction.play

Is the same, but terser:

    { FSinOsc.ar(220, 0.2) }.play

## Important Keyboard Commands

- Double-clicking right on a delimiter (parenthesis, brace, or bracket) selects the content between this delimiter and its match.
- Pressing the enter key executes (= evaluates) the selected code.
- Command-Z undoes the last action.
- Command-. (period) stops the execution of whatever is running.
- Command-8 cuts the volume by 60 dB.
- Command-9 cuts the volume by 3 dB.
- Command-0 boosts the volume by 3 dB.
- Command-P evaluates the selected code and prints the result.
- Command-H opens the help file for any Class.
- Command-Y tells you who understands a Message, and what arguments the method usually expects.

Two more shortcuts for the Curious and/or Courageous:

- The message '.inspect shows you what's inside any object.
- Selecting a Class name and typing Command-J opens the source code file for that Class.

## Help References

### Menus

You can double click on any square bracket to select text within the brackets indicating the topic of interest, and then type cmd-H to open the file.

- [File Menu]
- [Edit Menu]
- [Lang Menu]
- [GUI Menu]

- [Font Menu]
- [Size Menu]
- [Style Menu]
- [Color Menu]

- [Fill Menu]
- [Alter Menu]
- [Lib Menu]
- [Windows Menu]

### Main Help File

You can double click on any square bracket to select text within the brackets indicating the topic of interest, and then type cmd-H to open the file.

### Documentation

Intro

- [Getting Help]
- [How to Use the Interpreter]
- [01 Why SuperCollider]
- [02 What's New]

The Program

- [Menus]

The Language

- [00 Intro to Objects]
- [01 Literals]
- [02 Method Calls]
- [03 Assignment]
- [04 Comments]
- [05 Expression Sequence]
- [06 Functions]
- [07 Scope]
- [08 Control Structures]
- [09 Classes]

Audio Synthesis

- [01 UGens and Synths]
- [02 MultiChannel]

Other Topics

- [Open Sound Control]
- [LF vs Band Limited Oscils]
- [Internal Snooping]

### Help

Classes

- [Object]
- [AbstractFunction]
- [Boolean]
- [Char]
- [Collection]
- [Magnitude]
- [Nil]
- [Point]
- [Rect]
- [Ref]

Audio Classes

- [Synth]
- [UGen]

Unit Generator Classes

- [UGen\_Ref\_Sheet]
- [BinaryOp]
- [Controls]
- [Delays]
- [Filters]
- [Noise]
- [Oscillators]
- [Pan]
- [Spawn]
- [Triggers]
- [UnaryOp]
