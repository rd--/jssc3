# 1.5 Basic SC Syntax

As long as computers do not understand natural human language, we have to adhere to a well-defined convention for communication, a syntax.

Whenever you make syntax mistakes, SC2 will most likely catch them and give you error messages that are usually quite informative. Do read them!

When you can't find a mistake in the line SC2 complains about, the faulty code is usually above that line, and the problem only caused an execution obstacle later, in the line that SC2 points to.

Go through the doc under the Language [01 Literals], [02 Method Calls] etc.  for more details.

## Comments

Comments are probably the most important thing to write in a program.

````
/*
Anything enclosed between '/*' and '*/' is a comment and will be
ignored, i.e. SC2 will not consider it executable program code.
*/
(7 / 5).postln; 	// anything after '//' to the end of the line is also a comment.
````

It is highly recommended to document more than may you find necessary; you'll be glad you did when you try to read something you did two months ago at 3 in the morning, when it seemed like a great idea and crystal clear.

You can actually write a whole program as only comments first, then try to implement it line by line; that way you can always check your results against what you were planning to do.

## Variable Declarations

Variable declarations let you pick names for objects.  SC2 reserves Uppercase names for Classes only, so declaring

    var MyVariable; 		// will produce an error, while
    var myVariable; 		// is fine.

More precisely, variable names must start with a lowercase character:

    var 1Variable;  		// will also produce an error.

You can also assign a value to a variable while declaring it:

    var myVariable = 7;
    myVariable.postln;

but be aware that this only works with objects that have a direct ("literal") value, i.e. numbers, strings, symbols; see [01 Literals] for details.

Some examples:

````
var myVar = "This is a string."; 		// a string variable,
myVar.postln;

var myVar = 'aSymbol'; 			// a symbol variable.
myVar.postln;
````

examples for symbols;

````
var myVar = true; 					// a reserved word (true, false or nil).
myVar.postln;
`````

To create an array of literals directly, you can write an array preceded by '#':

````
var aVar = #[1,2,3];

var myVar = SinOsc;
myVar.postln;			// does not work, because SinOsc is not a literal.

var myVar;
myVar = SinOsc;
myVar.postln;			// works only when myVar was declared first.
````

SC2 also has 'ready-made' variable names: single lowercase characters from a-z can be used directly as variable names without declaring them.

````
a = 17;
a.postln;			// works.
````

While this saves you a lot of typing when you write something short to try an idea really quickly, it is not recommended for any larger program: Declaring and using descriptive variable names like panoramaPosition, modulationFrequency, reverbOutput etc.  makes the program readable to anybody and saves you a lot of typing in the documentation. ( Reasonable abbreviation is of course ok; I would probably write panPos, modFreq, and revOut etc.)

## Variable Scope

Where in the program you declare a variable determines where in the program you can access that variable; this is called the variable's scope.

````
(
var outerVar = 5, 		// define an outer var and
	aFunction;		// a function, i.e. a block of inside the program
					// that can be executed separately.
aFunction = { 			// just define that function, don't do it yet.
	var innerVar = 6; 		// define a var inside the function,
	 					// when  aFunction is executed:
	 outerVar = 4; 			// change the value of  outerVar;
	"// innerVar from inside aFunction: ".post;
	innerVar.postln;				// post innerVar, works from inside aFunction.
	"// innerVar + outerVar from inside aFunction: ".post;
	(innerVar + outerVar).postln;	// and post the sum of innerVar + outerVar.
};
"// outerVar before aFunction runs: ".post;
outerVar.postln;		// This line will post outerVar after  aFunction is executed.
aFunction.value;		// execute aFunction to post innerVar and
				// innerVar + outerVar.
"// outerVar after aFunction has executed: ".post;
outerVar.postln;		// This line will post outerVar after  aFunction is executed.

				// Try uncommenting these line: it will produce an error, because it tries
				// to post innerVar, which is only defined inside aFunction!
// innerVar.postln;
)
````

This is a very common source of errors.

See also [07 Scope].

## Assignments

Assignments let you give specific values to variables.  The general form here is <variable> = <some expression>.

An assignment is not an equation, you are only giving a name to an object; so you can not use an expression on the left hand side:

    a + 1 = 7; // error

Many Objects have instance variables that you can access directly; the usual methods for this are:

````
var a = Point.new;	// make a new Point; a Point has two instance variables called x and y.
a.x(3); 			// assign to the instance variable x (of point a) the value 3;
//	a.x_(3);		// this is fully equivalent to a.x = 3;
				// it sets the value x of Point a to 3.
a.x.postln;		// .<varName>  gets the current value of the instance variable.
````

I find a.x = 3 clearer for communication. There is a reason to write a.x_(3) though: It lets you chain more messages.

    a.x_(3).y_(4).asPolar.postln;


There are some shortcuts for multiple assignment, which are helpful if you use larger data sets; look those up in documentation [03 Assignment] if you're interested.

## Statements

Statements are the basic building blocks of programs in most languages.  In SC2 they are separated from each other by semicolons:

    a = 1;
    (a + 5).postln;

Writing them in the same line makes no difference:

    a = 1; (a + 5).postln;

The last statement in a sequence need not end with a semicolon, but it is not a mistake to write one anyway. If you add text later, a missing semicolon is a common source of error messages from the compiler.

    a = 1;
    (a + 4).postln
    b = a + 7

Did you see the missing semicolon here right away?

The result of a sequence of statements is always the result of the last statement!  This is especially important for functions:

````
Sc.play({
	1 + 3;
	SinOsc(220, 0) * 0.1;
})
````

This does not work:

````
Synth.play({
	SinOsc.ar(220, mul: 0.1);
	1 + 3;
})
````

How would you check what each function returns?

For more see also the doc file [05 Expression Sequence].

In a fully object-oriented environment such as SC2, statements usually have the form of Objects being sent (or passed):

## Messages

SC2 supports three kinds of writing messages:

- Binary Operator
- Message-Passing
- Functional Messages

See also the doc under [02 Method Calls].

Binary Operator messages consist of two expressions and a binary operator in the middle.

    2 + 3;
    (FSinOsc.ar(220) + PinkNoise.ar) * 0.2;

Note that the same operators can be used for numbers and unit generators, which are actually completely different objects: A number only has one value, while a UGen such as a sine oscillator puts out a signal of ever changing numbers, either at audio rate (44100 Hz) or at control rate (usually 64 times slower than that).

Sending the same messages to Objects of different classes is a central and very powerful concept of OOP called Polymorphism.  Among other things, it lets you use very natural language to tell objects what to do.

E.g. check out who understands the message play (using Command-Y).  Without going into too much detail, all of these objects are really different: A Function is roughly a piece of code inside curly brackets that waits to be evaluated, a SoundFile is ... (well, look it up yourself!), and a Synth is something else again.

All of them understand the message play to mean: do whatever is necessary to make myself heard somehow.

Message passing style: tell the string "Hi there!" to print itself,

    "Hi there!".postln; 	// <SomeObject>.<someMessage>;
    23.max(45);		// <SomeObject>.<someMessage>(<firstArgument>, <second>, ...);

Functional style is completely equivalent:

    postln("Lo there!");	// <someFunction>(<SomeObject>);
    max(23, 45);		// <someFunction>(<SomeObject>, <firstArgument>, <second>, ...);

This makes it easy to write things in the most readable way for you, e.g.

    sin(-1); 		// if you prefer that to
    -1.sin;

For more on messages and methods see also the SC doc [02 Method Calls].

Next: see an overview of all the sound generating Classes: [2.1 Unit Generators].
