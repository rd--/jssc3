# 2.4 Modulation Synthesis

These patches demonstrate some basic kinds of audio synthesis methods you may want to try: Amplitude, Phase, and Frequency Modulation.

## Amplitude Modulation

````
var carFreq = 200; // set the carrier oscillator freq
var carAmp = 0; // set the carrier amplitude
var modFreq = 327; // set the modulator oscillator freq
var modAmp = 0.1; // set the modulator amplitude
var modulator = SinOsc(modFreq, 0) * modAmp; // modulator  is a sine osc.
var carrier = SinOsc(carFreq, 0) * (carAmp + modulator) ; // carrier  is also a sine osc.
carrier
````

Try adding Mouse control to the frequencies or to the modulation amplitude.

## Phase Modulation

````
var carFreq = 200; // set the carrier oscillator freq
var carAmp = 0.1; // set the carrier amplitude
var modFreq = MouseY(1000, 10, 1, 0.2); // set the modulator oscillator freq
var modAmp = MouseX(0, 7, 0, 0.2); // set the modulator amplitude
var modulator = SinOsc(modFreq, 0) * modAmp; // modulator  is a sine osc.
var carrier = SinOsc(carFreq, modulator) * carAmp ; // carrier  is also a sine osc.
carrier
````

SC2 also implements this as one single UGen: see PMOsc (phase modulation oscillator pair).  This is exactly what the Yamaha DX7 synthesizer series did, so you could plug together more oscillators as given in the diagrams by Yamaha, and voila. (Maybe recreating the digital scratchiness and grit of the original DX7 would be the hardest thing about that project.)

## Frequency Modulation

There has been some dispute on what the "true" FM synthesis method is, see the article by Frode Holm (1992) in the [References].  The FM method that Yamaha has patented is actually phase modulation, but you may want to try actual Frequency Modulation too.  For sine waves, the results are the same as Phase Modulation if you rescale the modulator signal properly (which does make the math clumsier); for more complex modulators, there are differences.

````
var carFreq = 200; // set the carrier oscillator freq
var carAmp = 0.1; // set the carrier amplitude
var modFreq = MouseY(1000, 10, 1, 0.2); // set the modulator oscillator freq
var modAmp = MouseX(0, 1, 0, 0.2); // set the modulator amplitude
var modulator = SinOsc(modFreq, 0) * modAmp; // modulator  is a sine osc.
var carrier = SinOsc(carFreq + (modulator * carFreq), 0) * carAmp ; // carrier  is also a sine osc.
carrier
````

## Some Exercises

Try putting different envelopes as controllers over carrier and modulator amplitudes.  Write a soundFunction that has all the interesting params as arguments, then try putting it inside a Spawn object to play little sequences from lists; try using envelopes for carFreq and modFreq also; try putting them inside an OrcScore object.

Next: [2.5 Subtractive Synthesis]
