# 2.5 Subtractive Synthesis

Subtractive Synthesis is a traditional synthesis method based on a rich signal (i.e. with a strong overtone content) to start with and ways to reduce (or filter) that richness to achieve the desired sound.

Many legendary electronic music instruments operated this way, Moog, ARP, and Sequential Circuits synthesizers, older Roland models and countless others.

A classical rich sound source, the sawtooth oscillator:

    Saw(400) * 0.1

Square waves are done with the Pulse oscillator:

    Pulse(freq: 400, width: 0.5) * 0.1

The Pulse oscillator can also be "width-modulated":

    var y = MouseY(5000, 50, 1, 0.2); // frequency mouse control (vertical)
    var x = MouseX(0.01, 0.99, 0, 0.2);	// pulse width mouse control (horizontal)
    Pulse(y, x) * 0.1

The SC2 implementation of these oscillators is actually a vast improvement on the standard way these are done, because it bandlimits the frequency range of the signal to avoid digital artifacts called aliasing. See [LF vs Band Limited Oscils], and also the tutorial file [3.1 Testing - Ask the System!] for more.

These overtone-rich sounds are then filtered to achieve more musically useful sounds. Some standard filters are:

A LowPassFilter at audio rate with an input signal to the filter and a cutoff frequency:

    LPF(Saw(200), 800) * 0.1

Control cutoff frequency with a SinOsc:

````
LPF(
	Saw(200),	// an input signal
	SinOsc(0.5,	// 0.5 Hz modulation frequency
		0)	// no phase offset
	* 1000		// cutoff frequency oscillates +- 1000, i.e. 1000 Hz,
	+ 1500		// around an added center frequency of 1500 Hz
) * 0.2
````

A more interesting filter adds resonance to the signal: (taken from the doc file on [Filters])

````
RLPF(				// Resonant Low Pass Filter
	Saw(200) * 0.2,		// input signal to the filter
	SinOsc(			// control cutoff frequency with a SinOsc
		XLn(0.7,	// frequency controlled by line generator, start: 0.7Hz
			300,	// end: 300 Hz
			20),	// dur: 20 seconds
		0)		// SinOsc: no phase offset
	* 3600			// oscillate between +- 3600 Hz cutoff freq control value.
	+ 4000, 		// add offset of 4000 Hz to cutoff freq control.
	1			// filter quality
) * 0.2
````

You can also filter out the lower part of the frequency spectrum with a High Pass Filter:

````
var cutoffFreq = MouseY(10000, 50, 1, 0.2);
HPF(in: Saw(200) * 0.2, freq: cutoffFreq)
````

Try comparing Highpass and Lowpass filters side by side:


````
var cutoffFreq = MouseY(10000, 50, 1, 0.2);
var balance = MouseX(0, 1, 0, 0.2);
var source = Saw(200) * 0.2;
[
	HPF(source, cutoffFreq) * (1 - balance),
	LPF(source, cutoffFreq) * balance
]
````

James McCartney has made some interesting and fun examples using these sounds to recreate electronic 'period music'; note the use of a compositional algorithm Ugen, Sequencer: (from the Examples 4 file)

````
// berlin 1977 - mouse x controls clock rate
var clockRate = MouseX(5, 20, 1, 0.2);
var clockTime = 1 / clockRate;
var clock = Impulse(clockRate, 0);
var patternList = #[55, 60, 63, 62, 60, 67, 63, 58];
var note = DmdOn(clock, 0, Seq(inf, patternList));
var clock16 = PulseDivider(clock, 16, 0);
var noteTrs = DmdOn(clock16, 0, Seq(inf, [-12, -7, -5, 0, 2, 5])) + note;
var freq = noteTrs.midiCps;
var env = Decay2(clock, 0.05 * clockTime, 2 * clockTime);
var amp = env * 0.1 + 0.02;
var filt = env * (FSinOsc(0.17, 0) * 800) + 1400;
var pw = SinOsc(0.08, [0, 0.5 * pi]) * 0.45 + 0.5;
var s = Pulse(freq, pw) * amp;
CombC(RLPF(s, filt, 0.15), 0.2, [0.2, 0.17], 1.5)
````

# Exercises

If you have written some Spawn exercises with FM sound functions, try replacing their sound functions with subtractive ones; or just add filters to taste to FM patches you have written.

Next: [2.6 OrcScore - Using Notelists]
